FUNCTION zfm_gp_wms_pp_createruku1.
*"----------------------------------------------------------------------
*"*"本地接口：
*"  IMPORTING
*"     VALUE(WERKS) TYPE  WERKS_D
*"     VALUE(CORRELATIONNO) TYPE  AUFNR
*"     VALUE(PRODUCTCODE) TYPE  MATNR
*"     VALUE(JSQTY) TYPE  MENGE_D
*"     VALUE(UNIT) TYPE  MEINS
*"     VALUE(WMSNO) TYPE  LGORT_D
*"     VALUE(WMSPC) TYPE  CHAR30
*"     VALUE(MWMGZDATE) TYPE  DATS OPTIONAL
*"  EXPORTING
*"     VALUE(FLAG) TYPE  BAPI_MTYPE
*"     VALUE(MESSAGE) TYPE  BAPI_MSG
*"     VALUE(RTYPE) TYPE  BAPI_MTYPE
*"     VALUE(RTMSG) TYPE  BAPI_MSG
*"----------------------------------------------------------------------
  zfmdatasave1 'ZFM_GPBX_MES_PP_CREATERUKU'.
  zfmdatasave2 'B'.
  COMMIT WORK.
  "bapi 参数
  DATA:ls_header           TYPE          bapi2017_gm_head_01,
       ls_code             TYPE          bapi2017_gm_code,
       lt_item             TYPE TABLE OF bapi2017_gm_item_create,
       ls_item             TYPE          bapi2017_gm_item_create,
       lt_return           TYPE TABLE OF bapiret2,
       ls_return           TYPE          bapiret2,
       lv_materialdocument TYPE bapi2017_gm_head_ret-mat_doc,
       lv_matdocumentyear  TYPE bapi2017_gm_head_ret-doc_year.
  DATA:it_char TYPE TABLE OF zsmms_char WITH HEADER LINE,
       p_type  TYPE bapi_mtype,
       p_msg   TYPE bapi_msg.
  " 物料号加前导零  12.10.2022 15:39:59 by kkw
  DATA matnr1 TYPE matnr.

  ls_code = '02'.
  "准备抬头数据
  ls_header-pstng_date    = sy-datum.
  ls_header-doc_date      = sy-datum.             "凭证中的凭证日期
  ls_header-pr_uname      = sy-uname.              "用户名
  ls_header-header_txt = '博兴MES'.
  CLEAR:ls_item,lt_item.
  "行项目数据

  ls_item-orderid    =    |{ correlationno ALPHA = IN }|.         "工单号

  SELECT COUNT( * ) FROM afko WHERE aufnr = ls_item-orderid.
  IF sy-subrc NE 0.
    DATA(msg) = 'SAP查无工单:' && ls_item-orderid.
    fill_msgbx 'E' msg .
  ENDIF.

  CALL FUNCTION 'CONVERSION_EXIT_MATN1_INPUT'
    EXPORTING
      input  = productcode
    IMPORTING
      output = matnr1
*     EXCEPTIONS
*     LENGTH_ERROR       = 1
*     OTHERS = 2
    .
  "库存地点和工厂映射
  SELECT * INTO TABLE @DATA(lt_zsap2wms) FROM  zsap2wms.
  READ TABLE lt_zsap2wms INTO DATA(lw_zsap2wms) WITH KEY zwerks = werks zlgort = wmsno+0(3).
  IF sy-subrc EQ 0.
    werks = lw_zsap2wms-werks.
    wmsno = lw_zsap2wms-lgort.
  ELSE.
    DATA(msg3) = '工厂' && werks && '库存地' && wmsno && '没有映射关系'.
    fill_msgbx 'E' msg3 .
  ENDIF.
  ls_item-material   =    matnr1.         "物料编号
  ls_item-plant      =    werks.         "工厂
  ls_item-stge_loc   =    wmsno.         "库存地点
  CALL FUNCTION 'ZFM_GP_PP_MIGO_BATCHNO'
    EXPORTING
      aufnr = ls_item-orderid
    CHANGING
      charg = ls_item-batch.
*  ls_item-batch      =    ''.         "批次号
  ls_item-entry_qnt  =    jsqty.         "以录入项单位表示的数量
  ls_item-entry_uom  =    unit.
*  ls_item-move_stloc =    wmsno.         "收货库存地点 跨工厂转批次用
*  ls_item-move_batch =    ''.         "收货批次
*  ls_item-stck_type  = '2' . "质检状态
  ls_item-mvt_ind    =    'F'.             "移动标识  ’B‘为采购收货  ’F ' 生产收货
  ls_item-move_type  =    '101'.                 "移动类型
  APPEND ls_item  TO lt_item .

  CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
    EXPORTING
      goodsmvt_header  = ls_header
      goodsmvt_code    = ls_code
    IMPORTING
      materialdocument = lv_materialdocument
      matdocumentyear  = lv_matdocumentyear
    TABLES
      goodsmvt_item    = lt_item
      return           = lt_return.

  IF ( line_exists( lt_return[ type = 'A' ] ) ) OR ( line_exists( lt_return[ type = 'E' ] ) ) OR ( line_exists( lt_return[ type = 'X' ] ) ).
    CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.

    DATA: lt_msg_tab TYPE esp1_message_tab_type,
          wa_msg_tab LIKE LINE OF lt_msg_tab.
    DATA:msg1 TYPE bapi_msg.
    CLEAR msg1.
    LOOP AT lt_return INTO ls_return WHERE type CA 'AEX'.
      CONCATENATE '错误：' ls_return-message INTO msg1.
    ENDLOOP.
    flag = 'E'.
    message = msg1.
  ELSE.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.
    DATA(cc) = |101生产收货成功,凭证号:{ lv_materialdocument }|.
    flag = 'S'.
    message = cc.
  ENDIF.
  " 更新批次特性  11.10.2022 17:54:13 by kkw
  IF flag = 'S'.
    CLEAR : it_char, it_char[],p_type,p_msg.
    it_char-no = 'ZCPBH'.
    it_char-value = wmspc.
    APPEND it_char.
    CALL FUNCTION 'ZMMS_UPDATE_OBJCL'
      EXPORTING
        matnr  = matnr1
        charg  = ls_item-batch
        werks  = werks
      IMPORTING
        rtype  = p_type
        rtmsg  = p_msg
      TABLES
        char   = it_char
      EXCEPTIONS
        OTHERS = 1.

    IF p_type = 'S'.
      CONCATENATE message '更新批次特性成功.'  INTO message SEPARATED BY '/'.
    ELSE.
      CONCATENATE message '更新批次特性失败.'  INTO message SEPARATED BY '/'.
    ENDIF.
  ENDIF.
  zfmdatasave2 'R'.




ENDFUNCTION.
