FUNCTION ZFM_GP_SD_WMS_FHTZTB.
*"----------------------------------------------------------------------
*"*"本地接口：
*"  IMPORTING
*"     VALUE(VBELN) TYPE  VBELN
*"  EXPORTING
*"     VALUE(RTYPE) TYPE  BAPI_MTYPE
*"     VALUE(RTMSG) TYPE  BAPI_MSG
*"  TABLES
*"      INTAB1 STRUCTURE  ZSSD207 OPTIONAL
*"      INTAB2 STRUCTURE  ZSSD208 OPTIONAL
*"----------------------------------------------------------------------

  ZFMDATASAVE1 'ZFM_GP_SD_WMS_FHTZTB'.
  ZFMDATASAVE2 'B'.


  TYPES: BEGIN OF ZSLGORT ,
           WERKS TYPE T001W-WERKS,
           NAME1 TYPE T001W-NAME1,
           LGORT TYPE T001L-LGORT,
           LGOBE TYPE T001L-LGOBE,
         END OF ZSLGORT.

  DATA:GT_LGORT TYPE TABLE OF ZSLGORT    WITH HEADER LINE,
       P_STATUS TYPE I,
       P_INPUT  TYPE STRING,
       P_OUTPUT TYPE STRING,
       IN_TAB1  TYPE TABLE OF ZSSD207 WITH HEADER LINE,
       IN_TAB2  TYPE TABLE OF ZSSD208 WITH HEADER LINE.


  IF VBELN IS INITIAL.
    RTYPE = 'E'.
    RTMSG = '发货通知单号未输！'.
    ZFMDATASAVE2 'R'.
    RETURN.
  ENDIF.

  SELECT
   VBAK~VBELN,
   VBAK~AUART,
   KNA1~KUNNR,
   KNA1~NAME1,
   KNA1~REGIO,
   T005U~BEZEI,
   KNA1~ORT01,
   ADRC~STR_SUPPL3,
   ADRC~LOCATION,
*   ( T005U~BEZEI && '省' && ' ' && KNA1~ORT01  && ' ' && ADRC~STR_SUPPL3 && ' ' && ADRC~LOCATION ) AS DZ,
   VBAK~ERNAM,
   VBAK~ERDAT,
   VBAK~ERZET,
   TVKOT~VKORG,
   TVKOT~VTEXT,
   PRPS~PSPNR,
   PRPS~POSID,
   PRPS~POST1,
   VBAK~ZSJXM,
   VBAK~ZSJDH,
   VBAK~ZCHEHAO,
   VBAK~ZYSGS
   INTO TABLE @DATA(IT_VBAK)
   FROM VBAK
   INNER JOIN KNA1  ON VBAK~KUNNR = KNA1~KUNNR
   INNER JOIN VBKD  ON VBAK~VBELN = VBKD~VBELN      AND VBKD~POSNR = '000000'
   INNER JOIN ADRC  ON KNA1~ADRNR = ADRC~ADDRNUMBER
   LEFT  JOIN PRPS  ON VBAK~PS_PSP_PNR = PRPS~PSPNR
   LEFT  JOIN TVKOT ON VBAK~VKORG = TVKOT~VKORG     AND TVKOT~SPRAS = @SY-LANGU
   LEFT  JOIN T005U ON KNA1~REGIO = T005U~BLAND     AND T005U~SPRAS = @SY-LANGU  AND T005U~LAND1 = 'CN'
   WHERE VBAK~VBELN = @VBELN
*     AND VBAK~AUART IN ('ZBV1','ZDK1','ZDP1','ZFK1','ZFP1','ZIC1','ZRK1','ZRP1','ZTK1','ZTP1','ZWK1','ZWP1')
    AND VBAK~VBTYP = 'C' "发货
     "发货单
     .
*  CL_DEMO_OUTPUT=>DISPLAY( IT_VBAK ).
  IF SY-SUBRC <> 0 .
    RTYPE = 'E'.
    RTMSG = '未查询到该发货通知单号！'.
    ZFMDATASAVE2 'R'.
    RETURN.
  ENDIF.

  SELECT
   HTP~VBELN  AS HTD,
   HTP~POSNR  AS HTDH,
   VBAP~POSNR AS FHDH,
   VBKD~BSTKD,
   VBAP~MATNR,
   T001L~LGORT,
   T001L~LGOBE,
   VBAP~KWMENG,
   VBAP~VRKME,
   PRPS~PSPNR,
   PRPS~POSID,
   PRPS~POST1,
   VBAP~WERKS,
   T001W~NAME1
   INTO TABLE @DATA(IT_VBAP)
   FROM VBAP
   INNER JOIN VBKD ON VBKD~VBELN = VBAP~VBELN AND VBKD~POSNR = '000000'
   INNER JOIN VBAP AS HTP ON HTP~VBELN = VBAP~VGBEL AND HTP~POSNR = VBAP~VGPOS
   INNER JOIN T001W ON T001W~WERKS = VBAP~WERKS
   LEFT  JOIN PRPS  ON VBAP~PS_PSP_PNR = PRPS~PSPNR
   LEFT  JOIN T001L ON VBAP~LGORT = T001L~LGORT     AND VBAP~WERKS = T001L~WERKS
   WHERE VBAP~VBELN = @VBELN
*     AND VBAP~WERKS IN ('3060','3062')"镇江工厂
     .
  IF SY-SUBRC <> 0 .
    RTYPE = 'E'.
    RTMSG = '该通知单号不需推送WMS！'.
    ZFMDATASAVE2 'R'.
    RETURN.
  ENDIF.
  SORT IT_VBAP BY FHDH.
*  CL_DEMO_OUTPUT=>DISPLAY( IT_VBAP ).


  CLEAR:INTAB1[],INTAB2[].
  LOOP AT IT_VBAP INTO DATA(WA_VBAP).
    CLEAR : INTAB2,GT_LGORT.
    MOVE-CORRESPONDING WA_VBAP TO INTAB2.
    APPEND INTAB2.
    GT_LGORT-WERKS = WA_VBAP-WERKS.
    GT_LGORT-NAME1 = WA_VBAP-NAME1.
    GT_LGORT-LGORT = WA_VBAP-LGORT.
    GT_LGORT-LGOBE = WA_VBAP-LGOBE.
    COLLECT GT_LGORT.
  ENDLOOP.

  READ TABLE IT_VBAK INTO DATA(WA_VBAK) INDEX 1.
  CLEAR INTAB1.
  MOVE-CORRESPONDING WA_VBAK TO INTAB1.
  PERFORM GETLONGTEXT(ZPUBFORM) USING 'Z104' INTAB1-VBELN 'VBBK' CHANGING INTAB1-ZWLXZ.
  LOOP AT GT_LGORT .
    INTAB1-ZXH = SY-TABIX.
    INTAB1-WERKS = GT_LGORT-WERKS.
    INTAB1-LGORT = GT_LGORT-LGORT.
    INTAB1-LGOBE = GT_LGORT-LGOBE.
    APPEND INTAB1.
  ENDLOOP.

  LOOP AT INTAB1 .
    CLEAR : IN_TAB1 , IN_TAB1[] , IN_TAB2 ,IN_TAB2[].
    MOVE-CORRESPONDING INTAB1 TO IN_TAB1.
    APPEND IN_TAB1.
    LOOP AT INTAB2 WHERE LGORT = INTAB1-LGORT  AND WERKS = INTAB1-WERKS.
      CLEAR :  IN_TAB2 .
      MOVE-CORRESPONDING INTAB2 TO IN_TAB2.
      APPEND IN_TAB2.
    ENDLOOP.

    CALL FUNCTION 'ZFM_GP_SD_WMS_FHTZTB_POST'
      IMPORTING
*       P_INPUT =
*       P_OUTPUT =
*       P_STATUS =
        RTYPE  = RTYPE
        RTMSG  = RTMSG
      TABLES
        INTAB1 = IN_TAB1
        INTAB2 = IN_TAB2.
    IF RTYPE <> 'S'.
      RTMSG = '推送WMS第' && INTAB1-ZXH && '次失败！' && RTMSG.
      RETURN.
    ENDIF.
  ENDLOOP.


  RTYPE = 'S'.
  RTMSG = '推送WMS成功！'.


  ZFMDATASAVE2 'R'.


ENDFUNCTION.
