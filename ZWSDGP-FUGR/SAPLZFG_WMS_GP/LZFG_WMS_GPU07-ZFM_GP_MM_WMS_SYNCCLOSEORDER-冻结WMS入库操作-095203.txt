FUNCTION ZFM_GP_MM_WMS_SYNCCLOSEORDER.
*"----------------------------------------------------------------------
*"*"本地接口：
*"  IMPORTING
*"     REFERENCE(WERKS) TYPE  WERKS_D
*"     REFERENCE(LGORT) TYPE  LGORT_D
*"     REFERENCE(ZDJLX) TYPE  ZTMM201-ZDHLX DEFAULT 'RK'
*"     REFERENCE(ZDHNO)
*"     REFERENCE(ZCZR)
*"  EXPORTING
*"     VALUE(P_OUTPUT) TYPE  STRING
*"     VALUE(P_STATUS) TYPE  I
*"     VALUE(P_RESULT) TYPE  STRING
*"     VALUE(RTYPE) TYPE  BAPI_MTYPE
*"     VALUE(RTMSG) TYPE  BAPI_MSG
*"     VALUE(FLAG) TYPE  BAPI_MTYPE
*"     VALUE(MESSAGE) TYPE  BAPI_MSG
*"----------------------------------------------------------------------

  ZFMDATASAVE1 'ZFM_GP_MM_WMS_SYNCCLOSEORDER'.
  ZFMDATASAVE2 'B'.
  TYPES: BEGIN OF t_JSON1,
           WAREHOUSECODE TYPE I,
           ORDERTYPE     TYPE STRING,
           ORDERCODE     TYPE STRING,
           OPERUSERNAME  TYPE STRING,
         END OF t_JSON1.

  TYPES: BEGIN OF t_JSON2,
           ID         TYPE STRING,
           ERROR_CODE TYPE STRING,
           STATUS     TYPE STRING,
           MSG        TYPE STRING,
           PDF_URL    TYPE STRING,
           PRINT_URL  TYPE STRING,
         END OF t_JSON2.

  DATA:ITAB      TYPE t_JSON1,
       ITRE      TYPE t_JSON2,
       WMSURL    TYPE STRING,
       WMSMSG    TYPE STRING,
       WMSSTA    TYPE I,
       WMSSTR_RE TYPE STRING.
  CLEAR:ITAB,ITRE.
  SELECT * INTO TABLE @DATA(LT_ZSAP2WMS) FROM ZSAP2WMS.
*转换工厂和库存地
  READ TABLE LT_ZSAP2WMS INTO DATA(LW_ZSAP2WMS) WITH KEY WERKS = WERKS LGORT = LGORT.
  IF SY-SUBRC EQ 0.
    ITAB-WAREHOUSECODE = LW_ZSAP2WMS-ERPLGORT.
  ELSE.
    DATA(MSG3) = '工厂' && WERKS && '库存地' && LGORT && '没有映射关系'.
    FILL_MSGBX 'E' MSG3.
  ENDIF.
*  itab-warehousecode = lgort.
  ITAB-ORDERTYPE     = ZDJLX.
  ITAB-ORDERCODE     = ZDHNO.
  ITAB-OPERUSERNAME  = ZCZR.
  PERFORM GETDATA(ZPUB_DATA) USING 'ZFM_GP_MM_WMS_SYNCCLOSEORDER' CHANGING WMSURL.
  CHECK WMSURL IS NOT INITIAL.
  DATA(WMSSTR) = /UI2/CL_JSON=>SERIALIZE( DATA = ITAB  COMPRESS = ABAP_FALSE PRETTY_NAME = /UI2/CL_JSON=>PRETTY_MODE-CAMEL_CASE ).
  P_OUTPUT = WMSSTR.

*调用函数HTTP

  PERFORM REPLACE(ZPUBFORM) USING 'https' 'http' CHANGING WMSURL.
  CALL FUNCTION 'ZFMS_15_HTTP'
    EXPORTING
      INPUT     = WMSSTR
      URL       = WMSURL
      REQMETHOD = 'POST' "HTTP 方法
      HTTP1_1   = 'X' "协议1.1/1.0
    IMPORTING
      OUTPUT    = WMSSTR_RE "返回JSON报文
      RTMSG     = WMSMSG "消息
      STATUS    = WMSSTA "HTTP状态
    EXCEPTIONS
      OTHERS    = 1.
*返回报文转为SAP结构
*{
*    "id": null,
*    "errorCode": "",
*    "status": "success",
*    "msg": "操作成功！",
*    "pdfUrl": null,
*    "printUrl": null
*}

  /UI2/CL_JSON=>DESERIALIZE( EXPORTING JSON = WMSSTR_RE PRETTY_NAME = /UI2/CL_JSON=>PRETTY_MODE-CAMEL_CASE CHANGING DATA = ITRE ).

  P_STATUS = WMSSTA.
  P_RESULT = WMSSTR_RE.

  RTYPE = ITRE-STATUS.
  RTMSG = ITRE-MSG.


  ZFMDATASAVE2 'R'.


ENDFUNCTION.
