FUNCTION ZFM_LLJG_MOVE_CANCEL.
*"----------------------------------------------------------------------
*"*"本地接口：
*"  IMPORTING
*"     REFERENCE(ZDHDH) TYPE  ZTMM205-ZDHDH
*"     REFERENCE(BUDAT) TYPE  BUDAT DEFAULT SY-DATUM
*"     REFERENCE(ACT) TYPE  CHAR10
*"  EXPORTING
*"     REFERENCE(RTYPE) TYPE  BAPI_MTYPE
*"     REFERENCE(RTMSG) TYPE  BAPI_MSG
*"     REFERENCE(O_ZTMM205) TYPE  ZTMM205
*"  TABLES
*"      RETURN STRUCTURE  BAPIRET2 OPTIONAL
*"      T_ITEM STRUCTURE  ZTMM206 OPTIONAL
*"----------------------------------------------------------------------
  TABLES:ZTMM205,ZTMM206,ZVMM205.
  DATA:ITEM       TYPE TABLE OF BAPI2017_GM_ITEM_04 WITH HEADER LINE,
       IT_ZSMM206 TYPE TABLE OF ZSMM206 WITH HEADER LINE,
       IT_ZSPP006 TYPE TABLE OF ZSPP_006 WITH HEADER LINE.
  DATA:XBLNR TYPE XBLNR,
       ATNAM TYPE ATNAM,
       BKTXT TYPE BKTXT.
  CLEAR:O_ZTMM205,ZTMM206,RETURN[],ITEM[],RTYPE,RTMSG,
  IT_MIGO[],ZVMM205,IT_ZSMM206[],IT_ZSPP006[].
  SELECT *
    INTO TABLE @DATA(IT_ZVMM205)
    FROM ZVMM205
    WHERE ZDHDH = @ZDHDH
    AND   DEL NE 'X'.
  IF IT_ZVMM205[] IS INITIAL.
    FILLMSG 'E' '通知单不存在/已删除' 'X'.
  ENDIF.
  READ TABLE IT_ZVMM205 INTO ZVMM205 INDEX 1.
  MOVE-CORRESPONDING ZVMM205 TO O_ZTMM205.
  O_ZTMM205-BUDAT = BUDAT.
*取合同号对应项目
  SELECT SINGLE *
    FROM VBAK
    WHERE VBELN = O_ZTMM205-VBELN.
  SELECT SINGLE *
    FROM PRPS
    WHERE PSPNR = VBAK-PS_PSP_PNR.
  IF T_ITEM[] IS INITIAL.
    LOOP AT IT_ZVMM205 INTO ZVMM205 WHERE DELI NE 'X'.
      CLEAR:T_ITEM.
      MOVE-CORRESPONDING ZVMM205 TO T_ITEM.
      APPEND T_ITEM.
    ENDLOOP.
  ELSE.
    LOOP AT T_ITEM.
      READ TABLE IT_ZVMM205 INTO ZVMM205 WITH KEY ZDHDH = T_ITEM-ZDHDH
                                                  ZDHHH = T_ITEM-ZDHHH
                                                  BINARY SEARCH.
      IF SY-SUBRC EQ 0.
        T_ITEM-DEL = ZVMM205-DELI.
      ENDIF.
      MODIFY T_ITEM.
    ENDLOOP.
  ENDIF.
  IF T_ITEM[] IS INITIAL.
    FILLMSG 'E' '通知单不存在/已删除' 'X'.
  ENDIF.
  CASE ACT.
    WHEN 'POST'.
      IF O_ZTMM205-GZ = 'X'.
        FILLMSG 'E' '委外发料单状态不允许过账' 'X'.
      ENDIF.
*计算出米率
      PERFORM CALZCML IN PROGRAM ZRMM212A TABLES T_ITEM.
      LOOP AT T_ITEM WHERE DEL NE 'X'.
        CLEAR:IT_ZSPP006, IT_MIGO,ATNAM.
        IT_ZSPP006-ZJH = T_ITEM-ZJH.
        COLLECT IT_ZSPP006.
        IT_MIGO-MATNR = T_ITEM-MATNR.
        IT_MIGO-WERKS = O_ZTMM205-WERKS.
        IT_MIGO-MENGE = T_ITEM-ZJSL.
        IF STRLEN( T_ITEM-ZZBH ) GT 10.
          IT_MIGO-CHARG = T_ITEM-ZZBH.
        ENDIF.
        IT_MIGO-LGORT = T_ITEM-LGORT.
        CASE O_ZTMM205-ZKCLX.
          WHEN 'Q'.
            IT_MIGO-SOBKZ = O_ZTMM205-ZKCLX.
            IT_MIGO-PSPNR = PRPS-PSPNR.
        ENDCASE.
*写批次特征
        CONCATENATE T_ITEM-ZDHDH T_ITEM-ZDHHH T_ITEM-MATNR
        INTO IT_MIGO-SGTXT.
        SELECT SINGLE *
          FROM MARM
          WHERE MATNR = T_ITEM-MATNR
          AND   KZWSO = 'B'.
        IF MARM-ATINN IS NOT INITIAL.
          CALL FUNCTION 'CONVERSION_EXIT_ATINN_OUTPUT'
            EXPORTING
              INPUT  = MARM-ATINN
            IMPORTING
              OUTPUT = ATNAM.
          FILLPCTX IT_MIGO-SGTXT ATNAM  T_ITEM-ZCML.
        ENDIF.
        FILLPCTX IT_MIGO-SGTXT 'Z02' T_ITEM-ZJH.
        FILLPCTX IT_MIGO-SGTXT 'Z01' T_ITEM-ZZBH.
        FILLPCTX IT_MIGO-SGTXT 'Z10' T_ITEM-ZJSL.
        FILLPCTX IT_MIGO-SGTXT 'Z11' T_ITEM-ZRKL.
*        FILLPCTX IT_MIGO-SGTXT 'ZTM' T_ITEM-ZCML.
        FILLPCTX IT_MIGO-SGTXT 'Z08' T_ITEM-ZKW.
        FILLPCTX IT_MIGO-SGTXT 'Z38' O_ZTMM205-BUDAT.
        FILLPCTX IT_MIGO-SGTXT 'Z16' T_ITEM-ZBZH.
        FILLPCTX IT_MIGO-SGTXT 'Z24' PRPS-POSID.
        FILLPCTX IT_MIGO-SGTXT 'Z09' PRPS-POST1.
        FILLPCTX IT_MIGO-SGTXT 'Z25' O_ZTMM205-VBELN.
        FILLPCTX IT_MIGO-SGTXT 'Z13' SY-UNAME.
        FILLPCTX IT_MIGO-SGTXT 'Z00' T_ITEM-RKDW.
        APPEND IT_MIGO.
      ENDLOOP.
*若未过账，则检查自编号
      CALL FUNCTION 'ZFM_CHECKUSE_BYJH'
        EXPORTING
          ATNAM  = 'Z01'
        TABLES
          INTAB  = IT_ZSPP006
          OUTTAB = IT_ZSMM206.
      DELETE IT_ZSMM206 WHERE MBLNR IS INITIAL.
      SORT IT_ZSMM206 BY Z02.
      LOOP AT T_ITEM WHERE DEL NE 'X'.
        READ TABLE IT_ZSMM206 WITH KEY Z02 = T_ITEM-ZJH BINARY SEARCH.
        IF SY-SUBRC EQ 0.
          PERFORM MSGTOTEXT(ZPUBFORM) USING 'ZMSG_GP' '039'
                T_ITEM-ZDHHH
                 T_ITEM-ZJH IT_ZSMM206-MBLNR IT_ZSMM206-CHARG
                 CHANGING RTMSG.
          FILLMSG 'E' RTMSG 'X'.
        ENDIF.
      ENDLOOP.
      CALL FUNCTION 'ZFMS_01_MIGOPOST'
        EXPORTING
          BUDAT  = O_ZTMM205-BUDAT
          BKTXT  = BKTXT
          XBLNR  = XBLNR
          CODE   = '05' "'06'
          BWART  = '501' "'511'
        IMPORTING
          RTYPE  = RTYPE
          RTMSG  = RTMSG
          MBLNR  = O_ZTMM205-MBLNR
          MJAHR  = O_ZTMM205-MJAHR
        TABLES
          ITEM   = IT_MIGO
          RETURN = RETURN
        EXCEPTIONS
          OTHERS = 1.
      IF RTYPE NE 'E'.
        FILLMSG 'S' RTMSG ''.
*更新
        O_ZTMM205-CXMBLNR = ''.
        O_ZTMM205-CXMJAHR = ''.
        O_ZTMM205-GZ      = 'X'.
        UPDATE ZTMM205
        SET BUDAT = O_ZTMM205-BUDAT
            MBLNR = O_ZTMM205-MBLNR
            MJAHR = O_ZTMM205-MJAHR
            CXMBLNR = O_ZTMM205-CXMBLNR
            CXMJAHR = O_ZTMM205-CXMJAHR
            GZ      = O_ZTMM205-GZ
        WHERE ZDHDH = ZDHDH.
        COMMIT WORK.
      ELSE.
        FILLMSG 'E' RTMSG 'X'.
      ENDIF.
    WHEN 'CANCEL'.
      IF O_ZTMM205-GZ NE 'X'.
        FILLMSG 'E' '委外发料单状态不允许冲销' 'X'.
      ENDIF.
      IF O_ZTMM205-MBLNR IS NOT INITIAL.
        PERFORM MBST(ZPUBFORM) TABLES ITEM
          USING O_ZTMM205-MBLNR O_ZTMM205-MJAHR O_ZTMM205-BUDAT
                CHANGING RTMSG.
        RTYPE = RTMSG+0(1).
        IF RTYPE = 'S'.
          O_ZTMM205-CXMBLNR = RTMSG+2(10).
          O_ZTMM205-CXMJAHR = RTMSG+12(4).
        ENDIF.
      ENDIF.
      IF RTYPE NE 'E'.
        FILLMSG 'S' RTMSG ''.
        O_ZTMM205-MBLNR = ''.
        O_ZTMM205-MJAHR = ''.
        O_ZTMM205-GZ    = ''.
        UPDATE ZTMM205
        SET BUDAT = O_ZTMM205-BUDAT
            MBLNR = O_ZTMM205-MBLNR
            MJAHR = O_ZTMM205-MJAHR
            GZ    = O_ZTMM205-GZ
            CXMBLNR = O_ZTMM205-CXMBLNR
            CXMJAHR = O_ZTMM205-CXMJAHR
        WHERE ZDHDH = ZDHDH.
      ELSE.
        FILLMSG 'E' RTMSG 'X'.
      ENDIF.
  ENDCASE.
ENDFUNCTION.
