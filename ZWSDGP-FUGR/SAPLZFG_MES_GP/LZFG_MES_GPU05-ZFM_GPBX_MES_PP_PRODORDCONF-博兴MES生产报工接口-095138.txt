FUNCTION ZFM_GPBX_MES_PP_PRODORDCONF.
*"----------------------------------------------------------------------
*"*"本地接口：
*"  IMPORTING
*"     VALUE(WERKS) TYPE  WERKS_D
*"     VALUE(ERFZEIT_1) TYPE  BUCHDATUM
*"     VALUE(AUFNR) TYPE  AUFNR
*"     VALUE(LMNGA) TYPE  RU_LMNGA
*"     VALUE(VORNR) TYPE  VORNR
*"     VALUE(ISM01) TYPE  RU_ISMNG
*"     VALUE(ISM02) TYPE  RU_ISMNG
*"     VALUE(ISM03) TYPE  RU_ISMNG
*"     VALUE(ISM04) TYPE  RU_ISMNG
*"     VALUE(ISM05) TYPE  RU_ISMNG
*"  EXPORTING
*"     VALUE(FLAG) TYPE  BAPI_MTYPE
*"     VALUE(MESSAGE) TYPE  BAPI_MSG
*"     VALUE(RTYPE) TYPE  BAPI_MTYPE
*"     VALUE(RTMSG) TYPE  BAPI_MSG
*"----------------------------------------------------------------------
  ZFMDATASAVE1 'ZFM_GPBX_MES_PP_PRODORDCONF'.
  ZFMDATASAVE2 'B'.
  COMMIT WORK.

* 定义数据内表
  DATA: GT_AFKO TYPE STANDARD TABLE OF AFKO.
* Work Area 定义
  DATA: GW_AFKO TYPE AFKO,
        GW_AFPO TYPE AFPO,
        GW_AFVC TYPE AFVC,
        GW_AFVV TYPE AFVV.

  DATA: GV_INDEX  TYPE SY-TABIX,
        GV_RESULT TYPE C.
  DATA: ISM4 TYPE I,
        ISM5 TYPE I.
  DATA:
*BAPI相关定义
    PROPOSE            LIKE BAPI_PP_CONF_PROP,
    TIMETICKETS        LIKE BAPI_PP_TIMETICKET       OCCURS 0 WITH HEADER LINE,
    GOODSMOVEMENTS     LIKE BAPI2017_GM_ITEM_CREATE  OCCURS 0 WITH HEADER LINE,
    LINK_CONF_GOODSMOV LIKE BAPI_LINK_CONF_GOODSMOV  OCCURS 0 WITH HEADER LINE,
    RETURN_DETAIL      LIKE BAPI_CORU_RETURN         OCCURS 0 WITH HEADER LINE,
    RETURN             LIKE BAPIRET1                 OCCURS 0 WITH HEADER LINE.

  CONSTANTS:CN_FLAGX TYPE C VALUE 'X'.

  "报工取整
  ISM4 = ISM04.
  ISM5 = ISM05.
*RET_CODE
*ERR_MSG
*RET_TIME
* Get Data...
* Header
  CLEAR GT_AFKO[].
  AUFNR = |{ AUFNR ALPHA = IN }|.
  SELECT AUFNR RSNUM AUFPL
    INTO CORRESPONDING FIELDS OF TABLE GT_AFKO
    FROM AFKO
    WHERE AUFNR EQ AUFNR.  "生产工单号
  .
  IF SY-SUBRC NE 0.
    FILL_MSGBX 'E' '工单不存在'.
  ENDIF.
* Data Proccess...
  CLEAR GW_AFKO.
  READ TABLE GT_AFKO INTO GW_AFKO INDEX 1.

  IF SY-SUBRC = 0.

    CLEAR GW_AFVC.
    SELECT SINGLE
      AUFPL       "订单工序的工艺路线号
      APLZL       "订单的通用计数器
      VORNR       "工序
      PLNFL       "顺序
      ARBID
      APLFL       "序列（新） 有值
      INTO CORRESPONDING FIELDS OF GW_AFVC
      FROM AFVC
      WHERE AUFPL = GW_AFKO-AUFPL AND VORNR = VORNR.

    IF SY-SUBRC = 0.

* 1.需要更新的数据设置
      CLEAR PROPOSE.
      PROPOSE-QUANTITY          = CN_FLAGX.   "更新数量
      PROPOSE-DATE_AND_TIME     = CN_FLAGX.   "更新作业时间
      PROPOSE-GOODSMOVEMENT     = CN_FLAGX.   "更新货物移动

* 2.作业时间
      CLEAR: TIMETICKETS,TIMETICKETS[].
      TIMETICKETS-ORDERID       = AUFNR. "工单号
      TIMETICKETS-OPERATION     = GW_AFVC-VORNR.  "工序号
      TIMETICKETS-YIELD         = LMNGA. "工单生产数量
      TIMETICKETS-SEQUENCE      = GW_AFVC-APLFL.  "
      APPEND TIMETICKETS.
      CLEAR  TIMETICKETS.

* 3.货物移动属性
* 第一次赋值，用于获取相关默认属性值
      CLEAR: GOODSMOVEMENTS,GOODSMOVEMENTS[].
      GOODSMOVEMENTS-ORDERID    = AUFNR.  "生产订单号
      GOODSMOVEMENTS-ORDER_ITNO = GW_AFVC-VORNR.   "工序号
      APPEND GOODSMOVEMENTS.
      CLEAR  GOODSMOVEMENTS.

* 4.调用BAPI，获取默认属性值
      CALL FUNCTION 'BAPI_PRODORDCONF_GET_TT_PROP'
        EXPORTING
          PROPOSE            = PROPOSE
        IMPORTING
          RETURN             = RETURN
        TABLES
          TIMETICKETS        = TIMETICKETS
          GOODSMOVEMENTS     = GOODSMOVEMENTS
          LINK_CONF_GOODSMOV = LINK_CONF_GOODSMOV
          DETAIL_RETURN      = RETURN_DETAIL.

* 5.输出异常信息
      IF RETURN-TYPE CA 'AEX'.
        CONCATENATE '错误：' RETURN-MESSAGE INTO RETURN-MESSAGE.
        GV_RESULT = CN_FLAGX.
        LOOP AT RETURN_DETAIL WHERE TYPE CA 'AEX'.
          CONCATENATE '错误：' RETURN_DETAIL-MESSAGE INTO RETURN-MESSAGE.
          GV_RESULT = CN_FLAGX.
        ENDLOOP.
        FILL_MSGBX 'E' RETURN-MESSAGE .
      ENDIF.

* 6. 如若需要更新相关信息，可在此修改
* -- 这里修改作业时间 --
      IF GV_RESULT NE CN_FLAGX.

        CLEAR GV_INDEX.
        LOOP AT TIMETICKETS.

          GV_INDEX = SY-TABIX.    "索引
          CLEAR GW_AFVV.
          SELECT SINGLE * FROM AFVV
            INTO CORRESPONDING FIELDS OF GW_AFVV
            WHERE AUFPL = GW_AFVC-AUFPL
              AND APLZL = GW_AFVC-APLZL.

          IF SY-SUBRC = 0.
            TIMETICKETS-POSTG_DATE = ERFZEIT_1.
            TIMETICKETS-CONF_ACTI_UNIT1 = 'H'.
            TIMETICKETS-CONF_ACTIVITY1 = ISM01.
            TIMETICKETS-CONF_ACTI_UNIT2 = 'H'.
            TIMETICKETS-CONF_ACTIVITY2 = ISM02.
            TIMETICKETS-CONF_ACTI_UNIT3 = 'H'.
            TIMETICKETS-CONF_ACTIVITY3 = ISM03.
            TIMETICKETS-CONF_ACTI_UNIT4 = 'EA'.
            TIMETICKETS-CONF_ACTIVITY4 = ISM4.
            TIMETICKETS-CONF_ACTI_UNIT5 = 'EA'.
            TIMETICKETS-CONF_ACTIVITY5 = ISM5.
*              timetickets-conf_acti_unit6 = in_tab-ile06.
*              timetickets-conf_activity6 = in_tab-ism06.
          ENDIF.

          TIMETICKETS-YIELD  = LMNGA.    "待确认的产量（用户输入确认值）

          IF GW_AFVV IS NOT INITIAL.

            MODIFY TIMETICKETS INDEX GV_INDEX TRANSPORTING YIELD
              CONF_ACTIVITY1 CONF_ACTI_UNIT1 CONF_ACTIVITY2 CONF_ACTI_UNIT2
              CONF_ACTIVITY3 CONF_ACTI_UNIT3 CONF_ACTIVITY4 CONF_ACTI_UNIT4
              CONF_ACTIVITY5 CONF_ACTI_UNIT5
*                conf_activity6 conf_acti_unit6
              .

          ELSE.

            MODIFY TIMETICKETS INDEX GV_INDEX TRANSPORTING YIELD.

          ENDIF.

          CLEAR TIMETICKETS.
        ENDLOOP.

        CLEAR GW_AFVV.
      ENDIF.


* 7. 调用BAPI，报工确认
      CLEAR: RETURN, RETURN[].
      CLEAR: RETURN_DETAIL, RETURN_DETAIL[].
      CALL FUNCTION 'BAPI_PRODORDCONF_CREATE_TT'
        EXPORTING
          POST_WRONG_ENTRIES = '1'
          TESTRUN            = ''      "标识为 X，则为试运行 不提交至系统
        IMPORTING
          RETURN             = RETURN
        TABLES
          TIMETICKETS        = TIMETICKETS
          GOODSMOVEMENTS     = GOODSMOVEMENTS
          LINK_CONF_GOODSMOV = LINK_CONF_GOODSMOV
          DETAIL_RETURN      = RETURN_DETAIL.

      CLEAR GV_RESULT.
      IF RETURN-TYPE CA 'AEX'.

        CONCATENATE '错误：' RETURN-MESSAGE INTO RETURN-MESSAGE.
        GV_RESULT = CN_FLAGX.

        LOOP AT RETURN_DETAIL WHERE TYPE CA 'AEX'.

          CONCATENATE '错误：' RETURN_DETAIL-MESSAGE INTO RETURN-MESSAGE.
          GV_RESULT = CN_FLAGX.

        ENDLOOP.
        FILL_MSGBX 'E' RETURN-MESSAGE .
      ENDIF.

      IF GV_RESULT NE CN_FLAGX.
        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
          EXPORTING
            WAIT = CN_FLAGX.
        FILL_MSGBX 'S' '报工成功.'.
      ENDIF.

    ENDIF.
  ELSE.
    FILL_MSGBX 'E' '获取工序失败.'.
  ENDIF.

  CLEAR GW_AFKO.

  ZFMDATASAVE2 'R'.
ENDFUNCTION.
