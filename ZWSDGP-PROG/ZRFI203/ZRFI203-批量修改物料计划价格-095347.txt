*&---------------------------------------------------------------------*
*& Report ZRFI203
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT ZRFI203.
TYPE-POOLS:SLIS.
TABLES: SSCRFIELDS,MBEW.
DATA:FIELDCAT TYPE SLIS_T_FIELDCAT_ALV.
TYPES:BEGIN OF TY_OUT,
        WERKS TYPE WERKS_D,
        MATNR TYPE MATNR,
        ZPLP1 TYPE MBEW-ZPLP1,
        ZPLD1 TYPE MBEW-ZPLD1,
      END OF TY_OUT.
DATA: BEGIN OF ITAB OCCURS 0.
        INCLUDE TYPE TY_OUT.
DATA:   ICON TYPE ICON-ID,
        MSG  TYPE BAPI_MSG,
        FLAG TYPE BAPI_FLAG,
      END OF ITAB,
      IT_UP TYPE TABLE OF TY_OUT WITH HEADER LINE.
CONSTANTS:RED   TYPE ICON-ID VALUE '@0A@',
          GREEN TYPE ICON-ID VALUE '@08@'.
SELECTION-SCREEN BEGIN OF BLOCK B2 WITH FRAME TITLE T2.
  PARAMETERS: P_SEL  AS CHECKBOX MODIF ID M1.
SELECTION-SCREEN END OF BLOCK B2.

SELECTION-SCREEN FUNCTION KEY :1.

INITIALIZATION.





AT SELECTION-SCREEN OUTPUT.
  SSCRFIELDS-FUNCTXT_01 = '@14@导出模板'.
  %_P_SEL_%_APP_%-TEXT = '复制到剪切板（勿复制标题）'.

AT SELECTION-SCREEN.
  CASE SSCRFIELDS-UCOMM.
    WHEN 'FC01'.
      REFRESH FIELDCAT.
      PERFORM FILLFIELD.
      PERFORM ITABSTRUCTOCLIP(ZPUBFORM) USING FIELDCAT '' ''.
  ENDCASE.

START-OF-SELECTION.
  PERFORM SAVELOG(ZREPLOG) USING SY-REPID '' IF FOUND.
  PERFORM GETDATA.
  PERFORM ALVSHOW.


FORM GETDATA.
  IF P_SEL NE 'X'.
    MESSAGE S000(OO) WITH '请复制到剪切板后勾选' DISPLAY LIKE 'E'.
    STOP.
  ENDIF.
  PERFORM CLIPTOITAB(ZPUBFORM) TABLES IT_UP.
  LOOP AT IT_UP.
    CLEAR:MBEW, ITAB.
    MOVE-CORRESPONDING IT_UP TO ITAB.
    PERFORM ADDZERO_MATNR(ZPUBFORM) CHANGING ITAB-MATNR.
    SELECT SINGLE *
      FROM MBEW
      WHERE BWKEY = ITAB-WERKS
      AND   MATNR = ITAB-MATNR.
    IF SY-SUBRC NE 0.
      ITAB-FLAG = 'E'.
      ITAB-ICON = RED.
      ITAB-MSG = '未扩充财务视图'.
      APPEND ITAB.
      CONTINUE.
    ENDIF.
    IF MBEW-ZPLP1 = ITAB-ZPLP1
      AND MBEW-ZPLD1 = ITAB-ZPLD1.
      ITAB-FLAG = 'S'.
      ITAB-ICON = GREEN.
      ITAB-MSG = '无需更改'.
      APPEND ITAB.
      CONTINUE.
    ENDIF.
    APPEND ITAB.
  ENDLOOP.
  IF ITAB[] IS INITIAL.
    MESSAGE S000(OO) WITH '无数据' DISPLAY LIKE 'E'.
    STOP.
  ENDIF.
ENDFORM.

FORM ALVSHOW.
  REFRESH FIELDCAT.
  PERFORM INIT_FIELDCAT(ZPUBFORM) TABLES FIELDCAT USING:
'ICON' '状态' '' '' '' '',
'MSG' '消息' '' '' '' ''.
  PERFORM FILLFIELD.
  PERFORM ALVFM(ZPUBFORM) TABLES ITAB FIELDCAT USING 'X' ''.
ENDFORM.
FORM SET_STATUS USING RT_EXTAB TYPE SLIS_T_EXTAB.
  DATA LT_EXFCODE TYPE TABLE OF SY-UCOMM.
*  IF P4 = 'X'.
*    APPEND 'BUT1' TO LT_EXFCODE.
*  ENDIF.
  SET PF-STATUS 'STANDARD' EXCLUDING LT_EXFCODE.
ENDFORM. "set_status
FORM TOP_OF_PAGE.
  DATA:IT_LIST_COMMENTARY TYPE SLIS_T_LISTHEADER,
       WA_LIST_COMMENTARY TYPE SLIS_LISTHEADER,
       SJTMS              TYPE NUMC10.
  CLEAR:WA_LIST_COMMENTARY,SJTMS.
  REFRESH:IT_LIST_COMMENTARY.

  SJTMS = LINES( ITAB ).
  PERFORM DELZERO(ZPUBFORM) CHANGING SJTMS.

  WA_LIST_COMMENTARY-TYP = 'S'.
  WA_LIST_COMMENTARY-KEY = '条目数:'.
  WA_LIST_COMMENTARY-INFO = SJTMS.
  APPEND WA_LIST_COMMENTARY TO IT_LIST_COMMENTARY.

  CALL FUNCTION 'REUSE_ALV_COMMENTARY_WRITE'
    EXPORTING
      IT_LIST_COMMENTARY = IT_LIST_COMMENTARY[]
    EXCEPTIONS
      OTHERS             = 1.
ENDFORM.
FORM USER_COMMAND USING R_UCOMM LIKE SY-UCOMM
                        RS_SELFIELD TYPE SLIS_SELFIELD.
  DATA WA LIKE LINE OF ITAB.
  CASE R_UCOMM.
    WHEN '&IC1'.
      CHECK RS_SELFIELD-TABINDEX <> 0 . "小计行总计行什么的忽略
      READ TABLE ITAB INTO WA INDEX RS_SELFIELD-TABINDEX.
      CASE RS_SELFIELD-FIELDNAME.
        WHEN 'CHBOX'.
      ENDCASE.
    WHEN 'BUT01'."保存
      READ TABLE ITAB WITH KEY FLAG = 'E'.
      IF SY-SUBRC EQ 0.
        MESSAGE E000(OO) WITH '数据存在错误，请查看'.
      ENDIF.
      PERFORM BUT01.
  ENDCASE.
  RS_SELFIELD-ROW_STABLE = 'X'.
  RS_SELFIELD-COL_STABLE = 'X'.
  RS_SELFIELD-REFRESH = 'X'.
ENDFORM.
FORM FILLFIELD .
  PERFORM INIT_FIELDCAT(ZPUBFORM) TABLES FIELDCAT USING:
'WERKS' '工厂' '' '' '' '' ,
'MATNR' '物料' '' '' '' '' ,
'ZPLP1' '计划价格1' '' '' '' '' ,
'ZPLD1' '计划时间1' '' '' '' '' .
ENDFORM.                    " FILLFIELD
FORM BUT01.
  DATA:HEADDATA       TYPE BAPIMATHEAD,
       WA_RETURN      TYPE BAPIRET2,
       VALUATIONDATA  TYPE BAPI_MBEW,
       VALUATIONDATAX TYPE BAPI_MBEWX,
       RETURNMESSAGES TYPE TABLE OF BAPI_MATRETURN2 WITH HEADER LINE.

  LOOP AT ITAB WHERE FLAG IS INITIAL.
    CLEAR:HEADDATA,VALUATIONDATA,VALUATIONDATAX,
    WA_RETURN,RETURNMESSAGES[].

    HEADDATA-MATERIAL = ITAB-MATNR.
    HEADDATA-COST_VIEW = 'X'."成本视图
    HEADDATA-ACCOUNT_VIEW = 'X'."视图

    VALUATIONDATA-VAL_AREA = ITAB-WERKS.
    VALUATIONDATA-PLNDPRICE1 = ITAB-ZPLP1.
    VALUATIONDATA-PLNDPRDATE1 = ITAB-ZPLD1.
    PERFORM SETBAPIX(ZPUBFORM) USING VALUATIONDATA CHANGING VALUATIONDATAX.

    CALL FUNCTION 'BAPI_MATERIAL_SAVEDATA' DESTINATION 'NONE'
      EXPORTING
        HEADDATA       = HEADDATA
        VALUATIONDATA  = VALUATIONDATA
        VALUATIONDATAX = VALUATIONDATAX
      IMPORTING
        RETURN         = WA_RETURN
      TABLES
        RETURNMESSAGES = RETURNMESSAGES.
    LOOP AT RETURNMESSAGES WHERE TYPE CA 'AEX'.
      CONCATENATE RETURNMESSAGES-MESSAGE ITAB-MSG
      INTO ITAB-MSG SEPARATED BY '/'.
    ENDLOOP.
    IF SY-SUBRC EQ 0
      OR WA_RETURN-TYPE CA 'AEX'.
      PERFORM BAPIRUN(ZPUBFORM) USING ''.
      CONCATENATE WA_RETURN-MESSAGE ITAB-MSG
      INTO ITAB-MSG SEPARATED BY '/'.
      ITAB-ICON = RED.
    ELSE.
      PERFORM BAPIRUN(ZPUBFORM) USING 'X'.
      ITAB-ICON = GREEN.
      ITAB-MSG = '成功'.
      ITAB-FLAG = 'S'.
    ENDIF.
    MODIFY ITAB.
  ENDLOOP.


ENDFORM.
